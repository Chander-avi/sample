# VPC Module
module "vpc" {
  source = "../modules/vpc"

  vpc_name                        = var.vpc_name
  igw_name                        = var.igw_name
  nat_gateway_name_prefix         = var.nat_name_prefix
  public_route_table_name         = var.rt_public_name
  private_route_table_name_prefix = var.rt_private_name_prefix
  public_subnet_name_prefix       = var.subnet_public_name_prefix
  private_subnet_name_prefix      = var.subnet_private_name_prefix

  vpc_cidr             = var.vpc_cidr
  availability_zones   = var.availability_zones
  private_subnet_cidrs = var.subnet_private_cidrs
  public_subnet_cidrs  = var.subnet_public_cidrs

  tags = var.common_tags
}

# S3 Module
module "s3" {
  source = "../modules/s3"

  s3_bucket_names       = var.s3_bucket_names
  s3_versioning_enabled = var.s3_versioning_enabled

  tags = var.common_tags
}

# Secrets Manager Module
module "secrets_manager" {
  source = "../modules/secrets-manager"

  sm_secret_names        = var.sm_secret_names
  sm_secret_descriptions = var.sm_secret_descriptions
  sm_recovery_days       = var.sm_recovery_days

  tags = var.common_tags
}

# Lambda Module
module "lambda" {
  source = "../modules/lambda"

  vpc_id                  = module.vpc.vpc_id
  private_subnet_ids      = module.vpc.private_subnet_ids
  lambda_create           = var.lambda_create
  lambda_names            = var.lambda_names
  lambda_descriptions     = var.lambda_descriptions
  lambda_role_names       = var.lambda_role_names
  lambda_policy_names     = var.lambda_policy_names
  lambda_log_group_names  = var.lambda_log_group_names
  lambda_runtime          = var.lambda_runtime
  lambda_handler          = var.lambda_handler
  lambda_timeout          = var.lambda_timeout
  lambda_memory           = var.lambda_memory
  lambda_env_vars         = var.lambda_env_vars
  lambda_log_retention    = var.lambda_log_retention
  lambda_inline_policy    = var.lambda_inline_policy
  lambda_policy_arns      = var.lambda_policy_arns
  lambda_sg_name          = var.lambda_sg_name
  lambda_sg_description   = var.lambda_sg_description
  lambda_sg_ingress_rules = var.lambda_sg_ingress_rules
  lambda_sg_egress_rules  = var.lambda_sg_egress_rules

  tags = var.common_tags
}

# API Gateway Module
module "api_gateway" {
  source = "../modules/api-gateway"

  apigw_name                = var.apigw_name
  apigw_description         = var.apigw_description
  apigw_stage_name          = var.apigw_stage_name
  apigw_endpoint_type       = var.apigw_endpoint_type
  apigw_authorization_type  = var.apigw_authorization_type
  apigw_lambda_integrations = var.apigw_lambda_integrations
  apigw_method_types        = var.apigw_method_types
  lambda_invoke_arns        = module.lambda.lambda_invoke_arns
  apigw_cloudwatch_role_arn = module.iam_roles.iam_role_arns["apigw_cloudwatch_role"]
  apigw_log_retention       = var.apigw_log_retention
  apigw_logging_level       = var.apigw_logging_level
  apigw_data_trace_enabled  = var.apigw_data_trace_enabled

  tags = var.common_tags

  depends_on = [module.lambda, module.iam_roles, module.iam_policies]
}

# Connect Module
module "connect" {
  source = "../modules/connect"

  instance_alias           = var.connect_alias
  identity_management_type = var.connect_identity_type
  inbound_calls_enabled    = var.connect_inbound_enabled
  outbound_calls_enabled   = var.connect_outbound_enabled

  # KVS Configuration for Voicemail
  kvs_stream_prefix     = var.voicemail_enabled ? var.voicemail_kvs_stream_prefix : ""
  kvs_retention_hours   = var.voicemail_kvs_retention_hours
  kvs_encryption_key_id = var.voicemail_kvs_encryption_key_id

  tags = var.common_tags
}

# IAM Roles Module
module "iam_roles" {
  source = "../modules/iam-roles"

  iam_roles = var.iam_roles

  tags = var.common_tags
}

# IAM Policies Module
module "iam_policies" {
  source = "../modules/iam-policies"

  iam_policies           = var.iam_policies
  iam_policy_attachments = {}

  tags = var.common_tags

  depends_on = [module.iam_roles]
}

# IAM Policy Attachments (separate to avoid circular dependency)
resource "aws_iam_role_policy_attachment" "policy_attachments" {
  for_each = var.iam_policy_attachments

  role = each.value.role_name
  # If policy_arn doesn't start with "arn:", treat it as a policy key reference
  policy_arn = startswith(each.value.policy_arn, "arn:") ? each.value.policy_arn : module.iam_policies.iam_policy_arns[each.value.policy_arn]

  depends_on = [module.iam_roles, module.iam_policies]
}

# Kinesis Module
module "kinesis" {
  source = "../modules/kinesis"

  kinesis_streams = var.kinesis_streams

  tags = var.common_tags
}

# Glue Module
module "glue" {
  source = "../modules/glue"

  glue_databases = var.glue_databases
  glue_tables    = var.glue_tables

  tags = var.common_tags
}

# Analytics Lambda Module (for Firehose transformation and Users unload)
module "analytics_lambda" {
  source = "../modules/lambda"

  vpc_id                          = module.vpc.vpc_id
  private_subnet_ids              = module.vpc.private_subnet_ids
  lambda_create                   = var.analytics_lambda_create
  lambda_names                    = var.analytics_lambda_names
  lambda_descriptions             = var.analytics_lambda_descriptions
  lambda_role_names               = var.analytics_lambda_role_names
  lambda_policy_names             = var.analytics_lambda_policy_names
  lambda_log_group_names          = var.analytics_lambda_log_group_names
  lambda_runtime                  = var.analytics_lambda_runtime
  lambda_handler                  = var.analytics_lambda_handler
  lambda_timeout                  = var.analytics_lambda_timeout
  lambda_memory                   = var.analytics_lambda_memory
  lambda_timeout_per_function     = var.analytics_lambda_timeout_per_function
  lambda_memory_per_function      = var.analytics_lambda_memory_per_function
  lambda_env_vars                 = var.analytics_lambda_env_vars
  lambda_log_retention            = var.analytics_lambda_log_retention
  lambda_inline_policy            = var.analytics_lambda_inline_policy
  lambda_policy_arns              = var.analytics_lambda_policy_arns_common
  lambda_policy_arns_per_function = var.analytics_lambda_policy_arns
  lambda_sg_name                  = var.analytics_lambda_sg_name
  lambda_sg_description           = var.analytics_lambda_sg_description
  lambda_sg_ingress_rules         = var.analytics_lambda_sg_ingress_rules
  lambda_sg_egress_rules          = var.analytics_lambda_sg_egress_rules

  tags = var.common_tags
}

# Firehose Module
module "firehose" {
  source = "../modules/firehose"

  firehose_streams = {
    for k, v in var.firehose_streams : k => merge(v, {
      source_stream_arn         = module.kinesis.kinesis_stream_arns[v.kinesis_stream_key]
      destination_bucket_arn    = module.s3.s3_bucket_arns[v.s3_bucket_name]
      role_arn                  = module.iam_roles.iam_role_arns[v.iam_role_key]
      transformation_lambda_arn = module.analytics_lambda.lambda_arns[v.transformation_lambda_name]
    })
  }

  tags = var.common_tags
}


# ==========================================
# VOICEMAIL EXPRESS MODULES
# ==========================================

# Voicemail Lambda Layer (Python dependencies)
resource "aws_lambda_layer_version" "voicemail_python_layer" {
  count = var.voicemail_enabled ? 1 : 0

  layer_name          = var.voicemail_lambda_layer_name
  description         = var.voicemail_lambda_layer_description
  compatible_runtimes = var.voicemail_lambda_layer_runtimes
  license_info        = "https://aws.amazon.com/apache-2-0"

  s3_bucket = var.voicemail_lambda_layer_s3_bucket
  s3_key    = var.voicemail_lambda_layer_s3_key
}

# Voicemail Lambda Module
module "voicemail_lambda" {
  source = "../modules/lambda"
  count  = var.voicemail_enabled ? 1 : 0

  vpc_id                          = module.vpc.vpc_id
  private_subnet_ids              = module.vpc.private_subnet_ids
  lambda_create                   = var.voicemail_lambda_create
  lambda_names                    = var.voicemail_lambda_names
  lambda_descriptions             = var.voicemail_lambda_descriptions
  lambda_role_names               = var.voicemail_lambda_role_names
  lambda_policy_names             = var.voicemail_lambda_policy_names
  lambda_log_group_names          = var.voicemail_lambda_log_group_names
  lambda_runtime                  = var.voicemail_lambda_runtime
  lambda_handler                  = var.lambda_handler
  lambda_handler_per_function     = var.voicemail_lambda_handlers
  lambda_runtime_per_function     = {}
  lambda_timeout                  = 30
  lambda_memory                   = 128
  lambda_timeout_per_function     = var.voicemail_lambda_timeouts
  lambda_memory_per_function      = var.voicemail_lambda_memory
  lambda_env_vars                 = var.voicemail_lambda_env_vars
  lambda_log_retention            = var.voicemail_lambda_log_retention
  lambda_inline_policy            = {}
  lambda_policy_arns              = []
  lambda_policy_arns_per_function = var.voicemail_lambda_policy_arns_per_function
  lambda_sg_name                  = var.voicemail_lambda_sg_name
  lambda_sg_description           = var.voicemail_lambda_sg_description
  lambda_sg_ingress_rules         = {}
  lambda_sg_egress_rules          = {}
  lambda_vpc_enabled              = var.voicemail_lambda_vpc_enabled

  # S3 code deployment
  lambda_s3_bucket_per_function = {
    for name in var.voicemail_lambda_names : name => var.voicemail_lambda_code_bucket
  }
  lambda_s3_key_per_function = var.voicemail_lambda_s3_keys
  # Lambda layers - use created layer for functions that need it
  lambda_layers_per_function = {
    for name in var.voicemail_lambda_names : name => contains([
      "ip-connect-voicemail-kvs-to-s3-lambda-nonprod",
      "ip-connect-voicemail-transcriber-lambda-nonprod",
      "ip-connect-voicemail-packager-lambda-nonprod"
    ], name) ? [aws_lambda_layer_version.voicemail_python_layer[0].arn] : []
  }

  tags = var.common_tags

  depends_on = [aws_lambda_layer_version.voicemail_python_layer]
}

# Voicemail IAM User (for Presigner Lambda)
resource "aws_iam_user" "voicemail_presigner_user" {
  count = var.voicemail_enabled ? 1 : 0

  name = var.voicemail_presigner_user_name

  tags = merge(var.common_tags, {
    Name = var.voicemail_presigner_user_name
  })
}

# Voicemail IAM User Policy (S3 GetObject for presigned URLs)
resource "aws_iam_user_policy" "voicemail_presigner_policy" {
  count = var.voicemail_enabled ? 1 : 0

  name = "${var.voicemail_presigner_user_name}-policy"
  user = aws_iam_user.voicemail_presigner_user[0].name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "s3:GetObject"
        ]
        Resource = [
          "arn:aws:s3:::${var.voicemail_s3_recordings_bucket}/*"
        ]
      }
    ]
  })
}

# Voicemail IAM Access Key
resource "aws_iam_access_key" "voicemail_presigner_key" {
  count = var.voicemail_enabled ? 1 : 0

  user = aws_iam_user.voicemail_presigner_user[0].name
}

# Voicemail Secrets Manager (Store IAM credentials)
resource "aws_secretsmanager_secret" "voicemail_presigner_secret" {
  count = var.voicemail_enabled ? 1 : 0

  name        = var.voicemail_secrets_name
  description = "Stores IAM user credentials for voicemail presigner function"

  recovery_window_in_days = 7

  tags = merge(var.common_tags, {
    Name = var.voicemail_secrets_name
  })
}

resource "aws_secretsmanager_secret_version" "voicemail_presigner_secret_version" {
  count = var.voicemail_enabled ? 1 : 0

  secret_id = aws_secretsmanager_secret.voicemail_presigner_secret[0].id
  secret_string = jsonencode({
    MS_iam_key_id     = aws_iam_access_key.voicemail_presigner_key[0].id
    MS_iam_key_secret = aws_iam_access_key.voicemail_presigner_key[0].secret
  })
}

# Voicemail EventBridge Module
module "voicemail_eventbridge" {
  source = "../modules/eventbridge"
  count  = var.voicemail_enabled ? 1 : 0

  eventbridge_rules = {
    for k, v in var.voicemail_eventbridge_rules : k => merge(v, {
      target_arn = module.voicemail_lambda[0].lambda_arns[v.lambda_function_name]
    })
  }

  tags = var.common_tags

  depends_on = [module.voicemail_lambda]
}

# Voicemail Lambda Event Source Mapping (Kinesis CTR Stream)
module "voicemail_event_source" {
  source = "../modules/lambda-event-source"
  count  = var.voicemail_enabled ? 1 : 0

  event_source_mappings = {
    for k, v in var.voicemail_event_source_mappings : k => merge(v, {
      event_source_arn = module.kinesis.kinesis_stream_arns[var.voicemail_kinesis_ctr_stream_key]
      function_name    = module.voicemail_lambda[0].lambda_names[v.function_name]
    })
  }

  tags = var.common_tags

  depends_on = [module.voicemail_lambda, module.kinesis]
}

# Voicemail SES Email Templates
module "voicemail_ses" {
  source = "../modules/ses"
  count  = var.voicemail_enabled ? 1 : 0

  ses_templates = var.voicemail_ses_templates

  tags = var.common_tags
}
